plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'org.springframework.boot' version '3.1.0'
    id 'org.barfuin.gradle.jacocolog' version '1.0.1'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.fbc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', '2022.0.4')
    set('mapstructVersion', '1.4.2.Final')
    set('telegramBotsVersion', '6.0.1')
    set('testContainerPostgresVersion', '1.16.3')
    set('jupyterVersion', '1.16.2')
    set('jakartaModelgenVersion', '5.6.15.Final')
    set('jaxbApiVersion', '2.3.0')
    set('vavrVersion', '0.10.4')
}

dependencies {
    // Spring Web Starter
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'
    annotationProcessor "org.hibernate:hibernate-jpamodelgen-jakarta:${jakartaModelgenVersion}"
    implementation "javax.xml.bind:jaxb-api:${jaxbApiVersion}"

    // Feign Client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // MapStruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // Util
    implementation "io.vavr:vavr:${vavrVersion}"

    // Telegram API
    implementation "org.telegram:telegrambots-spring-boot-starter:${telegramBotsVersion}"

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:postgresql:${testContainerPostgresVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${jupyterVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}



tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

sonar {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'fbc-bot'
        property 'sonar.projectKey', 'fbc-bot'
    }
}
